buildscript {
    repositories {
        jcenter()
        google()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.2.1'
        classpath 'com.google.protobuf:protobuf-gradle-plugin:0.8.6'
    }
}


allprojects {
    repositories {
        jcenter()
        google()
    }
}

apply plugin: 'com.android.library'
apply plugin: 'com.google.protobuf'

android {
    compileSdkVersion 28
    buildToolsVersion '28.0.3'
    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 28
    }
    sourceSets {
        main {
            manifest.srcFile 'src/main/java/com/google/android/apps/common/testing/accessibility/framework/AndroidManifest.xml'
        }
    }
    lintOptions {
        abortOnError false
    }
    compileOptions {
        targetCompatibility 1.8
        sourceCompatibility 1.8
    }
}

protobuf {
    protoc {
        artifact = 'com.google.protobuf:protoc:3.5.1'
    }
    plugins {
        javalite {
            // The codegen for lite comes as a separate artifact
            artifact = 'com.google.protobuf:protoc-gen-javalite:3.0.0'
        }
    }
    generateProtoTasks {
        all().each { task ->
            task.builtins {
                // In most cases you don't need the full Java output
                // if you use the lite output.
                remove java
            }
            task.plugins {
                javalite {}
            }
        }
    }
}

dependencies {
    compile 'com.google.protobuf:protobuf-lite:3.0.1'
    compile 'androidx.legacy:legacy-support-v4:1.0.0'
    compile 'com.google.guava:guava:24.0-android'
    compile 'org.checkerframework:checker:2.3.2'
    compile 'org.hamcrest:hamcrest-library:1.3'
    compile 'org.hamcrest:hamcrest-core:1.3'
}

clean {
    delete 'src/main/generated'
}
